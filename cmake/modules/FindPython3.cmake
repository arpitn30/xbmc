# - Try to find python
# Once done this will define
#
# PYTHON3_FOUND - system has PYTHON
# PYTHON3_INCLUDE_DIRS - the python include directory
# PYTHON3_LIBRARIES - The python libraries

if(PKG_CONFIG_FOUND)
  pkg_check_modules(PC_PYTHON python>=3 QUIET)
endif()

find_program(PYTHON3_EXECUTABLE python ONLY_CMAKE_FIND_ROOT_PATH)
find_library(PYTHON3_LIBRARY NAMES python3.6 PATHS ${PC_PYTHON_LIBDIR})
find_path(PYTHON3_INCLUDE_DIR NAMES Python.h PATHS ${PC_PYTHON_INCLUDE_DIRS} ${DEPENDS_PATH}/${ARCH}/include/python3.6)

if(KODI_DEPENDSBUILD)
  find_library(FFI_LIBRARY ffi REQUIRED)
  find_library(EXPAT_LIBRARY expat REQUIRED)
  find_library(INTL_LIBRARY intl REQUIRED)
  find_library(GMP_LIBRARY gmp REQUIRED)

  if(NOT CORE_SYSTEM_NAME STREQUAL android)
    set(PYTHON_DEP_LIBRARIES pthread dl util)
  endif()

  set(PYTHON3_LIBRARIES ${PYTHON3_LIBRARY} ${FFI_LIBRARY} ${EXPAT_LIBRARY} ${INTL_LIBRARY} ${GMP_LIBRARY} ${PYTHON_DEP_LIBRARIES})
else()
  find_package(PythonLibs 3.6 REQUIRED)
  list(APPEND PYTHON3_LIBRARIES ${PC_PYTHON_STATIC_LIBRARIES})
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Python3 REQUIRED_VARS PYTHON3_INCLUDE_DIR PYTHON3_LIBRARY PYTHON3_LIBRARIES)
if(PYTHON3_FOUND)
  set(PYTHON3_INCLUDE_DIRS ${PYTHON3_INCLUDE_DIR})
  list(APPEND PYTHON3_DEFINITIONS -DHAS_PYTHON3=1)
endif()

mark_as_advanced(PYTHON3_EXECUTABLE PYTHON3_INCLUDE_DIRS PYTHON3_INCLUDE_DIR PYTHON3_LIBRARY PYTHON3_LIBRARIES PYTHON_LDFLAGS FFI_LIBRARY EXPAT_LIBRARY INTL_LIBRARY GMP_LIBRARY)

unset(PYTHON_EXECUTABLE)
unset(PYTHON_LIBRARY)
unset(PYTHON_INCLUDE_DIR)

unset(PYTHON_LIBRARIES)
unset(PYTHON_INCLUDE_DIRS)
unset(PYTHON_DEFINITIONS)

unset(PC_PYTHON)
unset(PC_PYTHON_LIBDIR)
unset(PC_PYTHON_INCLUDE_DIRS)
unset(PC_PYTHON_STATIC_LIBRARIES)
